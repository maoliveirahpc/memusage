filenames=system("ls -x memusage.*.out")
n=words(filenames)
filename(i)=word(filenames,i)

set terminal x11 enhanced persist title "Memory Usage" size 1000,1000 # choose terminal
set autoscale                          # scale axes automatically
unset log                              # remove any log-scaling
unset label                            # remove any previous labels
set xtics auto                         #
set xlabel "Iteration"                 #
set ytic auto                          # set ytics automatically
set ylabel "MB"
set key off
n1=sqrt(n)
n2=ceil(n/n1)
n1=floor(n1)
set multiplot layout n1,n2 title "Memory Usage"

do for [i=1:n] {
if ( i == n) { set key auto }
plot filename(i) using 0:($1/1024) title "VmSize" with linespoints, \
     filename(i) using 0:($2/1024) title "VmPeak" with linespoints, \
     filename(i) using 0:($3/1024) title "VmRSS"  with linespoints, \
     filename(i) using 0:($4/1024) title "VmHWM"  with linespoints
}

unset multiplot